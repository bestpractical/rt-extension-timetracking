<& /Elements/Header, Title => $user && $user->id != $session{CurrentUser}->id ? loc("[_1]'s Week", $user->Name) : loc("My Week") &>

<& /Elements/Tabs &>

<& /Elements/ListActions, actions => \@results &>

<script type="text/javascript">
jQuery( function() {
        jQuery("div.time_tracking input[name=Date]").datepicker( 'option', 'firstDay', <% ($week_start->Localtime('user'))[6] %> );
});

</script>
<div class="time_tracking">

<form>
% if ( $DefaultTimeUnits ) {
<input type="hidden" value="<% $DefaultTimeUnits %>" name="DefaultTimeUnits" />
% }
<table>
% if ( $session{CurrentUser}->HasRight( Object => $RT::System, Right => 'AdminTimesheets' )) {
<div>
<tr><td class="label">
<input type="hidden" name="User" value="<% $User || '' %>" />
<&|/l&>Go to user</&>:</td>
<td><input type="text" name="UserString" value="" data-autocomplete="Users" data-autocomplete-return="Name" id="autocomplete-User" /></td>
</tr>
</div>
% }
<tr><td class="label">
<&|/l&>Week of (pick any day in week)</&>:</td>
<td><& /Elements/SelectDate, ShowTime => 0, Name => 'Date', Default => $date->Date(Format=>'ISO', Timezone => 'user') &></td></tr>
</table>
</form>

% for my $day ( sort keys %week_worked ) {
<h2><% $week_worked{$day}{date}->RFC2822(Time => 0, Timezone => 'user') %></h2>

% if ( %{$week_worked{$day}{tickets}} ) {
<table class="ticket-list collection-as-table">
<tr class="collection-as-table">
<th class="collection-as-table"><&|/l&>id</&></th>
<th class="collection-as-table"><&|/l&>Subject</&></th>
<th class="collection-as-table"><&|/l&>Status</&></th>
<th class="collection-as-table"><&|/l&>Owner</&></th>
<th class="collection-as-table"><&|/l&>Time Worked</&></th>
<th class="collection-as-table"><&|/l&>Update</&></th>
</tr>
% my $i = 1;
% for my $ticket_id ( sort { $a <=> $b } keys %{$week_worked{$day}{tickets}} ) {
% my $entry = $week_worked{$day}{tickets}{$ticket_id};
% my $ticket = $entry->{ticket};
<tr class="<% $i++ % 2 ? 'oddline' : 'evenline' %>">
<td class="collection-as-table">
<a href="<% RT->Config->Get('WebPath') %>/Ticket/Display.html?id=<% $ticket->id %>"><% $ticket->id %></a>
</td>
<td class="collection-as-table">
<a href="<% RT->Config->Get('WebPath') %>/Ticket/Display.html?id=<% $ticket->id %>"><% $ticket->Subject %></a>
</td>
<td class="collection-as-table"><% $ticket->Status %></td>
<td class="collection-as-table"><% $ticket->OwnerObj->Name %></td>
<td class="collection-as-table"><& /Ticket/Elements/ShowTime, minutes => $entry->{time_worked} &></td>
<td class="collection-as-table">
<form method="POST">
    <input type="hidden" value="<% $day %>" name="Object-RT::Transaction--CustomField-<% $date_cf->id %>-Values" />
% if ( $user->id != $session{CurrentUser}->id ) {
    <input type="hidden" value="<% $session{CurrentUser}->id %>" name="Object-RT::Transaction--CustomField-<% $actor_cf->id %>-Values" />
% }
    <input name="id" type="hidden" value=<% $ticket->id %> />
    <& /Elements/EditTimeValue,
        Name    => "UpdateTimeWorked",
        Default => '',
        InUnits => $DefaultTimeUnits || 'minutes',
    &>
    <input type="submit" class="button" value="<% loc('Save') %>">
</form>

</td>
</tr>
% }
</table>

<div class="time_worked">
<span class="label"><&|/l&><% $week_worked{$day}{week_name} %> Total</&>:</span> <span class="value"><& /Ticket/Elements/ShowTime, minutes => $week_worked{$day}{time_worked} &></span>
</div>
% }

<form method="POST">
    <input type="hidden" value="<% $day %>" name="Object-RT::Transaction--CustomField-<% $date_cf->id %>-Values" />
% if ( $user->id != $session{CurrentUser}->id ) {
    <input type="hidden" value="<% $session{CurrentUser}->id %>" name="Object-RT::Transaction--CustomField-<% $actor_cf->id %>-Values" />
% }
    <&|/l&>Add ticket</&>: <input name="id" type="text" size="8" data-autocomplete="Tickets" />
    <&|/l&>Time Worked</&>:
    <& /Elements/EditTimeValue,
        Name    => "UpdateTimeWorked",
        Default => '',
        InUnits => $DefaultTimeUnits || 'minutes',
    &>
    <input type="submit" class="button" value="<% loc('Save') %>">
</form>

% }

<hr />
<div class="time_worked">
<span class="label"><&|/l&>Total Time Worked</&>:</span> <span class="value"><& /Ticket/Elements/ShowTime, minutes => $total_time_worked &></span>
</div>

% $m->callback( CallbackName => 'End', User => $user, Date => $date, WeekWorked => \%week_worked );

</div>
<%INIT>
my $user;
my @results;
if ( $User ) {
    if ( $session{CurrentUser}->HasRight( Object => $RT::System, Right => 'AdminTimesheets' ) ) {
        $user = RT::CurrentUser->new($session{CurrentUser}); 
        $user->Load($User);
        unless ( $user->id ) {
            push @results, loc("Could not load user [_1]", $User);
        }
    }
    else {
        push @results, loc("Permission denied");
    }
}
else {
    $user = $session{CurrentUser};
}

MaybeRedirectForResults(
    Actions   => \@results,
    Arguments => { Date => $Date, DefaultTimeUnits => $DefaultTimeUnits },
);

my $date_cf = RT::CustomField->new($user);
$date_cf->LoadByName( Name => 'Worked Date', LookupType => 'RT::Queue-RT::Ticket-RT::Transaction');

my $actor_cf = RT::CustomField->new($user);
$actor_cf->LoadByName( Name => 'Actor', LookupType => 'RT::Queue-RT::Ticket-RT::Transaction');

if ( defined $ARGS{'UpdateTimeWorked'} ) {
    RT::Interface::Web::PreprocessTimeUpdates(\%ARGS);
    my $ticket = RT::Ticket->new( $user );
    $ticket->Load($ARGS{id});

    if ( $ticket->id ) {
        my ( $val, $msg, $txn ) = $ticket->SetTimeWorked( $ticket->TimeWorked + $ARGS{'UpdateTimeWorked'} );
        push( @results, $msg );
        $txn->UpdateCustomFields( %ARGS ) if $txn;
    }
    else {
        push @results, loc("Could not load ticket $ARGS{id}");
    }

    MaybeRedirectForResults(
        Actions   => \@results,
        Arguments => { Date => $Date, DefaultTimeUnits => $DefaultTimeUnits, User => $User },
    );
}

my $date = RT::Date->new($user);
if ($Date) {
    $date->Set(Value => $Date, Format => 'unknown');
} else {
    $date->SetToNow;
}
$date->SetToMidnight( Timezone => 'user' );

my ($ret, $week_start) = RT::Extension::TimeTracking::WeekStartDate($user,
    $date, RT->Config->Get('TimeTrackingFirstDayOfWeek'));

my $week_end = RT::Date->new($user);
$week_end->Set( Value => $week_start->Unix );
$week_end->AddDays( 7 );

my %week_worked;

my @week_names = @RT::Extension::TimeTracking::DAYS_OF_WEEK;
if ( RT->Config->Get('TimeTrackingFirstDayOfWeek') ) {
    my $offset = $RT::Extension::TimeTracking::WEEK_INDEX{RT->Config->Get('TimeTrackingFirstDayOfWeek')};
    if ( $offset ) {
        @week_names = @week_names[$offset .. $#week_names, 0 .. $offset -1 ];
    }
}

for my $offset ( 0 .. 6 ) {
    my $date = RT::Date->new($user);
    $date->Set( Value => $week_start->Unix );
    $date->AddDays( $offset ) if $offset;
    my $user_hour = ($date->Localtime('user'))[2];
    if ( $user_hour == 23 ) {
        $date->AddSeconds(3600); # to get around isuses raised by daylight saving
    }

    $week_worked{$date->ISO(Time => 0, Timezone => 'user')} = {
        date => $date,
        week_name => $week_names[$offset],
        tickets => {},
    };
}

my $txns = RT::Transactions->new($user);
$txns->Limit(
    FIELD    => 'ObjectType',
    VALUE    => 'RT::Ticket',
);

$txns->Limit(
    FIELD    => 'Creator',
    VALUE    => $user->id,
);

$txns->Limit(
    FIELD    => 'TimeTaken',
    VALUE    => 0,
    OPERATOR => '!=',
);

$txns->Limit(
    FIELD    => 'Created',
    VALUE    => $week_start->ISO(),
    OPERATOR => '>=',
);
$txns->Limit(
    FIELD    => 'Created',
    VALUE    => $week_end->ISO(),
    OPERATOR => '<',
    ENTRYAGGREGATOR => 'AND',
);

my $total_time_worked = 0;
while ( my $txn = $txns->Next ) {
    my $ticket = $txn->Object;
    next if $txn->FirstCustomFieldValue('Worked Date'); # we handle this in the next part
    my $worked_date = $txn->CreatedObj->ISO( Time => 0, Timezone => 'user' );
    next unless $week_worked{$worked_date};
    $week_worked{$worked_date}{tickets}{$ticket->id} ||= {
        ticket => $ticket,
    };
    $week_worked{$worked_date}{tickets}{$ticket->id}{time_worked} += $txn->TimeTaken;
    $week_worked{$worked_date}{time_worked} += $txn->TimeTaken;
    $total_time_worked += $txn->TimeTaken;
}

$txns = RT::Transactions->new($user);
$txns->Limit(
    FIELD    => 'ObjectType',
    VALUE    => 'RT::Ticket',
);

$txns->Limit(
    FIELD    => 'Creator',
    VALUE    => $user->id,
);

$txns->Limit(
    FIELD    => 'TimeTaken',
    VALUE    => 0,
    OPERATOR => '!=',
);

my $cf = RT::CustomField->new($user);
$cf->Load('Worked Date');
my $cf_alias = $txns->Join(
        ALIAS1 => 'main',
        FIELD1 => 'id',
        TABLE2 => 'ObjectCustomFieldValues',
        FIELD2 => 'ObjectId'
);
$txns->Limit( ALIAS => $cf_alias, FIELD => 'CustomField', VALUE => $cf->id );
$txns->Limit( ALIAS => $cf_alias, FIELD => 'ObjectType', VALUE => 'RT::Transaction' );
$txns->Limit( ALIAS => $cf_alias, FIELD => 'Content', VALUE => $week_start->ISO(Time => 0), OPERATOR => '>=' );
$txns->Limit( ALIAS => $cf_alias, FIELD => 'Content', VALUE => $week_end->ISO(Time => 0), OPERATOR => '<', ENTRYAGGREGATOR => 'AND', );

while ( my $txn = $txns->Next ) {
    my $ticket = $txn->Object;
    my $worked_date = $txn->FirstCustomFieldValue('Worked Date');
    next unless $week_worked{$worked_date};
    $week_worked{$worked_date}{tickets}{$ticket->id} ||= {
        ticket => $ticket,
    };
    $week_worked{$worked_date}{tickets}{$ticket->id}{time_worked} += $txn->TimeTaken;
    $week_worked{$worked_date}{time_worked} += $txn->TimeTaken;
    $total_time_worked += $txn->TimeTaken;
}

my $activity_txns = RT::Transactions->new($user);

$activity_txns->Limit( FIELD => 'Creator', VALUE => $user->id );
$activity_txns->Limit( FIELD => 'ObjectType', VALUE => 'RT::Ticket' );
$activity_txns->Limit( FIELD => 'Created', OPERATOR => '>=', VALUE => $week_start->ISO );
$activity_txns->Limit( FIELD => 'Created', OPERATOR => '<',  VALUE => $week_end->ISO, ENTRYAGGREGATOR => 'AND' );
$activity_txns->Limit( FIELD => 'Type', VALUE => 'Create' );
$activity_txns->Limit( FIELD => 'Type', VALUE => 'Correspond' );
$activity_txns->Limit( FIELD => 'Type', VALUE => 'Comment' );
$activity_txns->GroupByCols( { FIELD => 'ObjectId' } );
my @ticket_ids;
while ( my $txn = $activity_txns->Next ) {
    my $ticket = $txn->Object;
    my $worked_date = $txn->CreatedObj->ISO( Time => 0, Timezone => 'user' );
    next unless $week_worked{$worked_date};
    next if $week_worked{$worked_date}{tickets}{$ticket->id};
    $week_worked{$worked_date}{tickets}{$ticket->id} = {
        ticket => $ticket,
        time_worked => 0,
    }
}

</%INIT>

<%ARGS>
$Date => undef
$DefaultTimeUnits => undef
$User => undef
</%ARGS>
